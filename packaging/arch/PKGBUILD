# Maintainer: Alf Gaida <agaida@siduction.org>

_pkgname=lxqt-kcm-integration
pkgname=$_pkgname-git
pkgver=0.0.0.5.g3b790ce
pkgrel=1
pkgdesc="LXQt KCM Integration"
arch=("i686" "x86_64")
url="http://lxqt.org"
license=("LGPL2")
depends=("kde-cli-tools" "kglobalaccel" "kwin" "plasma-workspace")
optdepends=('bluedevil: needed for lxqt-kcm-bluetooth'
            'kde-gtk-config: needed for lxqt-kcm-appearance'
            'kinfocenter: needed for lxqt-kcm-systeminfo'
            'plasma-desktop: needed for lxqt-kcm-appearance colors and styles'
            'kcm-wacomtablet: wacom tablet kcm support'
            'print-manager: printing kcm support'
	    'fancontrol-kcm: fancontrol kcm support'
	    'kcm-fcitx: fcitx input method kcm support'
	    'kcm-fcitx5-git: fcitx5 input method kcm support'
	    'plymouth-kcm: plymouth kcm support'
	    'sddm-kcm: sddm kcm support'
	    'kdeconnect: kdeconnect kcm support'
	    'plasma-thunderbolt: thunderbolt kcm support'
	    'nx-firewall-git: ufw firewall kcm support'
)
makedepends=("git" "cmake" "lxqt-build-tools-git")
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git+https://github.com/lxqt/$_pkgname.git")
sha256sums=("SKIP")


pkgver() {
	cd "$srcdir/$_pkgname"
	git describe --always | sed "s/-/./g"
}

build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$_pkgname" \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
